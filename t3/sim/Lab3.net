* AC/DC

* forces current values to be saved
.options savecurrents
.param ninverse=50/230

******************************************
*Independant voltage source (230 V amplitude; 50 Hz frequency) 
Vin 1 0 0 sin(0 230 50 0 0)

*Dependant voltage source
E2 3 4 1 0 ninverse

*Dependant current source
F2 1 0 E2 ninverse

*Full-wave rectifier
D13  3 out1  diode
D23  4 out1  diode
D41  0 3  diode
D42  0 4 diode

RL out1 0 10.75k
CL  out1 0  5000u

*Voltage regulator
RV out1 out2 5.10979k

Dv1  out2 1d  diode
Dv2 1d 2d  diode
Dv3 2d 3d  diode
Dv4 3d 4d  diode
Dv5 4d 5d  diode
Dv6 5d 6d  diode
Dv7 6d 7d  diode
Dv8 7d 8d  diode
Dv9 8d 9d  diode
Dv10 9d 10d  diode
Dv11 10d 11d  diode
Dv12 11d 12d  diode
Dv13 12d 13d  diode
Dv14 13d 14d  diode
Dv15 14d 15d diode
Dv16 15d 16d diode
Dv17 16d 0 diode

.MODEL  diode d

.control

*Color options
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0

*Plots
tran 0.1M 100M 0M 
hardcopy outsignals.ps v(out2) v(out1)
echo outsignals_FIG

tran 0.1M 400M 300M 
hardcopy outsignals_new.ps v(out2) v(out1)
echo outsignals_new_FIG
hardcopy out1signal.ps v(out1)
echo out1signal_FIG
hardcopy out2signal.ps v(out2)
echo out2signal_FIG
hardcopy deviation.ps (v(out2)-12)
echo deviation_FIG

*Tables
meas tran avgout AVG v(out2) from=300m to=400m
meas tran maxout MAX v(out2) from=300m to=400m
meas tran minout MIN v(out2) from=300m to=400m 

*Calculate cost and merit
let cost=10.75+5.10979+5000+0.1*(17+4)
let merit=1/(cost*(maxout-minout+abs(avgout-12)+0.000001))

echo "rip_TAB"
print avgout
print maxout
print minout
print (maxout-minout)
echo "rip_END"

echo "cost_merit_TAB"
print cost
print merit
echo "cost_merit_END"


quit
.endc


.end
