*
* AC/DC
*

* forces current values to be saved
.options savecurrents

******************************************
Vin 2 0 sin(0 230 50 0 0)
* This defines a sinusoid of 230 V amplitude and 50 Hz. 
RS 2 1 10
L1 1 01 2000
L2 3 01 61
K L1 L2 0.99999


D13  3 out1  diode
D23  01 out1  diode
D41  0 3  diode
D42  0 01  diode

CL  out1 0  400u
RL  out1 0  1k
RV out1 out2 21.6k
Dv1  out2 1d  diode
Dv2 1d 2d  diode
Dv3 2d 3d  diode
Dv4 3d 4d  diode
Dv5 4d 5d  diode
Dv6 5d 6d  diode
Dv7 6d 7d  diode
Dv8 7d 8d  diode
Dv9 8d 9d  diode
Dv10 9d 10d  diode
Dv11 10d 11d  diode
Dv12 11d 12d  diode
Dv13 12d 13d  diode
Dv14 13d 14d  diode
Dv15 14d 15d  diode
Dv16 15d 16d diode
Dv17 16d 17d diode
Dv18 17d 0 diode


.MODEL  diode d





.control

*makes plots in color
set hcopypscolor=0
set color0=white
set color1=black
set color2=red
set color3=blue
set color4=violet
set color5=rgb:3/8/0
set color6=rgb:4/0/0


tran 0.01M 300M 40m
hardcopy outsignals.ps v(out2) V(out1)
echo outsignals_FIG
hardcopy deviation.ps (v(out2)-12)
echo deviation_FIG

meas tran maxout MAX v(out2) from=50m to=300m
meas tran minout MIN v(out2) from=50m to=300m 
meas tran avgout AVG v(out2) from=50m to=300m

echo "rip_TAB"
print maxout
print minout
print (maxout-minout)
print avgout
echo "rip_END"

quit
.endc


.end
